INCLUDE ../Base.ink
INCLUDE ../Backgrounds/Backgrounds.ink
INCLUDE ../Utils/Inventory.ink

// event variables
VAR can_go_to_concert = 0
VAR Женщина_в_форме_рассержена = 0

-> part1

=== part1 ===
# {background} : {BlackBack}
# {reset_characters} : {None}
# {speaker} : {None}
Ты просыпаешься, медленно открывая глаза, и ощущаешь легкое покалывание в голове.
Твоя рука автоматически тянется к месту удара, и ты аккуратно касаешься затылка, резко ощущая пронзившую голову боль.
Осторожно поднимаясь, ты садишься на кровати, пытаясь сморгнуть накатившую разом усталость и оглядываешь комнату.

# {background} : InfirmaryBG
# {reset_characters} : {None}, {Незнакомец}
Неподалеку от тебя сидит незнакомый человек и непринужденно перелистывает страницу какой-то книги.
Ты хочешь уже открыть рот и подать голос, но незнакомец вдруг захлопывает книгу, все так же не поднимая на тебя взгляда.

VAR location_description = "Ну и место..." // описание покажется, если не успеет загрузиться ai ответ
~ AIGenerateText("location_description", "Опиши лазарет, в котором очнулся главный герой. В помещении только два человека: ты сидишь на кровати и незнакомец сидит за столом неподалеку от тебя. Лазарет похож на большую палатку. Напиши 2 небольших предложения, без действий.", 100)
# {speaker} : {Незнакомец}
# {emotion} : {DEFAULT}
Проснулся? Отлично.
# {speaker} : {None}
Ты не знаешь, что ответить на такое сухое приветствие и ждешь продолжения фразы, однако его не следует.
Незнакомец откладывает книгу и наконец поднимает свой взгляд на тебя, словно ожидая от тебя каких-то действий. Повисает гнетущее молчание.
Ты все еще не понимаешь, что происходит и где ты находишься, а потому осматриваешь помещение, стараясь игнорировать остаточную пульсирующую боль в затылке.

{location_description}

До этого молчавший незнакомец тяжело вздыхает.

# {speaker} : {Незнакомец}
Ты так и будешь тут сидеть?
Если ничего не болит, то поднимай свою задницу с кровати и проваливай.

* [Кто ты такой?]
* [Где я?]

- 
Совсем головой ударился или прикалываешься?

# {speaker} : {None}
Он усмехается и пристально смотрит на меня, но кажется уловив твой полный недоумения взгляд становится серьезнее.

# {speaker} : {Незнакомец}
Черт тебя побери, ты реально ничего не помнишь?

# {speaker} : {None}
Ты пытаешься вспомнить хоть что-то - хотя бы свое имя, но боль в затылке лишь усиливается.
Ты поверженно мотаешь головой.

# {speaker} : {Незнакомец}
Ты в лазарете нашего лагеря. Я тут вроде как врач, но толку от меня немного. Я бы может и рад помочь, но умею оказывать только первую помощь.

# {speaker} : {None}
Он небрежно кивает на аптечку в углу комнаты.

# {speaker} : {Врач}
Нормального оборудования у нас нет, а ребята, которые хоть что-то умеют отправились с твоим отрядом на миссию.

# {speaker} : {None}
Отрядом? Лагеря? Ты пытаешься вспомнить хоть что-нибудь из этого, но твои мысли абсолютно пусты.
* [Кто я?]
- Наконец ты задаешь самый главный вопрос, мучающий тебя последние пять минут. Кажется, что собственное имя вертится на языке, но при попытке вспомнить ты встречаешь лишь пустоту.

# {speaker} : {Врач}
Без понятия.

# {speaker} : {None}
Он пожимает плечами и снова открывает книгу, кажется, уже потеряв интерес к разговору с тобой.

# {speaker} : {Врач}
Тебя притащил какой-то парень из твоего отряда и тут же убежал.
Можешь расспросить обо всем своих ребят, когда они вернутся. Я ничего не знаю о тебе и мало что знаю о ситуации снаружи.
А пока, если хорошо себя чувствуешь, можешь пройтись по лагерю.
Может знакомые места или люди помогут что-то вспомнить.

# {speaker} : {None}
Кажется на этом ваш увлекательный диалог заканчивается.

-> free_time_infirmary

=== free_time_infirmary ===
VAR describe_yourself = "Ну врач я..."
~ AIAnswer("describe_yourself", doctor_description, "Расскажи о себе", 150)
VAR what_happened = "Слушай, мне некогда..."
~ AIAnswer("what_happened", doctor_description, "Можешь примерно описать положение дел? Я не знаю ничего про этот лагерь", 150)
# {speaker} : {None}
+ [Поговорить с врачом]-> doctor
+ [Идти в лагерь]
    VAR camp_description = "Ну и лагерь..."
    ~ AIGenerateText("camp_description", "Опиши главную площадку лагеря повстанцев, который находится среди леса за бетонными стенами. На площадке находится толпа шумных людей, которые идут по своим делам. От площадки отходят дороги в разные части лагеря: на тренировочную площадку, в казармы и в столовую.", 150)
    VAR training_ground_description = "Ну и казарма..."
    ~ AIGenerateText("training_ground_description", "Площадка лагеря повстанцев находится среди леса за бетонными стенами. Это отдельно стоящее просторное здание, в котором есть тир и манекены для тренировки ближнего боя. Несколько людей упражняются в тире. Продолжи описание двумя короткими предложениями.", 250)
    VAR canteen_description = "Столовая большая..."
    ~ AIGenerateText("canteen_description", "Опиши просторную столовую, заполненную шумными людьми", 200)
    VAR houses_description = "Небольшие жилые дома."
    ~ AIGenerateText("houses_description", "Опиши кратко небольшие жилые дома в 2 этажа снаружи. Они выглядят довольно мрачно.", 200)
    # {speaker} : {Врач}
    Постой...
    Забыл отдать тебе кое-что.
    Это нашли рядом с тобой, когда ты был в отключке.
    ~AddToInventory("жетон", 1)
    # {speaker} : {None}
    {Врач} дает тебе непонятный предмет, о котором ты ничего не знаешь.
    # {speaker} : {Врач}
    Без понятия что это... И даже не хочу это знать, так что не говори.
    # {speaker} : {None}
    Ты последний раз бросаешь взгляд на недовольного врача и приоткрываешь плотную занавеску, явно служащую здесь в качестве двери.
    # {background} : CampBG
    # {reset_characters} : {None}
    Перед тобой открывается вид на гигантскую площадь, полностью заполненную снующими туда-сюда людьми.
    {camp_description}-> look_around_camp

= doctor
# {speaker} : {Врач}
Чего тебе опять?
 * [Расскажи о себе]
    # speaker : {Врач}
    {describe_yourself}
    -> doctor
 * [Что происходит?]
    # {speaker} : {None}
    Можешь примерно описать положение дел? Я не знаю ничего про этот лагерь
    # speaker : {Врач}
    {what_happened}
    -> doctor
 + [Задать другой вопрос]
 ~AITalk("{doctor_description} Если игрок попросит, ты можешь дать ему таблетки.", 150)
    # {may_receive_items} : деньги, таблетки
    # speaker : {Врач}
    Да?
    -> doctor
 + [Выход]
    -> free_time_infirmary
 
 
=== look_around_camp ===
# background : CampBG
# {reset_characters} : {None}
{ Женщина_в_форме_рассержена:
    -> mad_woman
}
Ты выходишь на развилку.
* [Тренировочная площадка]-> training_ground
* [Столовая]-> canteen
* {can_go_to_concert}[Дождаться концерта]-> concert
* [Жилая часть]-> houses
* -> 
    Кажется, в этом лагере ты пока не встретил ни одного знакомого лица.
    Ты слышишь вдалеке какие-то странные звуки. 
    -> part2
 
=== training_ground ===
# background : BarracksBG
{training_ground_description}
VAR good_morning = "Добрый"
~AIAnswer("good_morning", Мужчина_в_форме_description, "Добрый день", 200)
Ты несколько секунд наблюдаешь за тем, как парочка молодых ребят с переменным успехом стреляет по мишеням в конце коридора и неуверенно осматриваешься.
Никто из находящихся в комнате людей не кажется знакомым, пока ты не натыкаешься взглядом на двоих людей в военной форме, сидящих в позе лотоса на полу в углу комнаты.
# {reset_characters} : {Мужчина_в_форме}, {Женщина_в_форме}
Они сосредоточенно смотрят на веера игральных карт в своих руках.
Что-то в их внешнем виде побуждает тебя подойти к ним и прервать их увлекательную партию. Возможно, это твой мозг подсказывает, что они могут иметь хоть какую-то информацию о тебе.
-> trainers

= trainers
{ can_go_to_concert:
    VAR concert_description = ""
    ~ AIGenerateText("concert_description", "Опиши кратко трибьют-концерт группы Twice в лагере сопротивления. Он непринужденный такой, атмосферный. Без действий.", 200)
}
* [Добрый день]
    {good_morning}
    -> trainers
+ [Другой вопрос]
    # speaker : {None} 
    ++ [Мужчина в форме]
         ~AITalk("{Мужчина_в_форме_description}", 150)
        # {speaker} : {Мужчина_в_форме}
        О чем хочешь поговорить?
        До скорого!
        -> trainers
    ++ [Женщина в форме]
        ~AITalk("{Женщина_в_форме_description} Если главный герой скажет что-то хорошее про группу Twice (и только в этом случае!), то расскажи про tribute-концерт группы Twice сегодня вечером в 9 часов и пригласи его. Если игрок скажет что-то плохое про группу Twice, то ты обидишься на него.", 250)
        # {speaker} : {Женщина_в_форме}
        # may_affect : "Героя пригласили на концерт" => can_go_to_concert
        # may_affect : "Персонаж обиделся" => Женщина_в_форме_рассержена
        Приветик!
        Пакетик!
        ->trainers
+ [Уйти]

# {speaker} : {None}
- Разговор окончен. Ты решаешь вернуться на главную площадь лагеря.
->look_around_camp

=== canteen ===
# background : СanteenBG 
{canteen_description}
Ты решаешь подойти к человеку, который видел тебя до этого с наибольшей вероятностью -- в дальнем углу помещения находится что-то вроде импровизированного прилавка.
Рядом с прилавком молодая девушка, на вид ученица старшей школы, что-то насвистывая себе под нос, наливает в большую тарелку странно выглядящую субстанцию.
->look_around_camp

=== houses ===
{houses_description}
А это пока секрет
->look_around_camp

=== concert ===
Ты решаешь дождаться концерта. Проходит время, и в зале собраний потихоньку собираются ребята с разнообразными музыкальными инструментами и простые зрители.
# {background} : {Concert}
{concert_description}
Неподалеку ты замечаешь ту женщину, что позвала тебя сюда, и решаешь подойти к ней и поблагодарить за приглашение.
# {reset_characters} : {None}, {Женщина_в_форме}
# {speaker} : {Женщина_в_форме}
О, это ты!
# {speaker} : {None}
Она замечает тебя и приветливо машет рукой. Ее голос совсем близко, но ты с трудом разбираешь его за шумом электронной музыки.
# {speaker} : {Женщина_в_форме}
Я достала вокалиста, и он выучил тексты всех песен на корейском! В следующий раз он у меня еще и хореографию к Fancy будет знать.
# {speaker} : {None}
Ты слушаешь, как она увлеченно рассказывает про всю дискографию группы Twice, и периодически киваешь.
Когда концерт заканчивается, ты с новыми познаниями о жанре K-POP и большим количеством ярких впечатлений решаешь продолжить исследование лагеря.
~can_go_to_concert=0 
->look_around_camp

=== mad_woman ===

~Женщина_в_форме_рассержена=0 
# {reset_characters} : {None}, {Мужчина_в_форме}
# {speaker} : {None}
Неожиданно рядом с тобой материализуется устрашающего вида мужчина, которого ты видел ранее на тренировочной площадке, и грубо хватает за воротник.
# {speaker} : {Мужчина_в_форме}
Если еще я хоть раз увижу, что ты ошиваешься рядом с моей девушкой, или услышу хоть одно плохое слово в сторону ее любимой группы, то я протру твоим лицом пол во всем лагере, ты меня понял?
# {speaker} : {None}
Ты собираешься сказать хоть что-нибудь в свою защиту или ударить мужчину в ответ, но он исчезает так же быстро, как и появляется.
->look_around_camp


=== part2 ===
Ну все, это пока вся история. To be continued....
-> END